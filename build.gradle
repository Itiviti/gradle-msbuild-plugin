import org.gradle.internal.os.OperatingSystem;

plugins {
    id 'nu.studer.plugindev' version '1.0.3'
    id "com.jfrog.bintray" version "1.2"
    id "com.ullink.msbuild" version "2.10"
    id "com.ullink.nuget" version "2.7"
}

group = 'com.ullink.gradle'

apply plugin: 'groovy'

description 'Gradle plugin for MSBuild project build.'

ext.dotnetPath = file('src/main/dotnet')

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0', transitive: false
    compile 'net.java.dev.jna:jna:3.5.0'
    compile 'net.java.dev.jna:platform:3.5.0'
    compile 'com.google.guava:guava:16.0.1'
    compile 'commons-io:commons-io:2.4'
}

msbuild.dependsOn nugetRestore
msbuild.onlyIf { OperatingSystem.current().windows }
msbuild {
    solutionFile = file('ProjectFileParser.sln')
    configuration = 'Release'
    projectName = 'ProjectFileParser'
}

task generateExe(dependsOn: msbuild, type: Exec) {
    workingDir = msbuild.mainProject.getProjectPropertyPath('OutputPath')
    commandLine = [file('packages/ILRepack.1.26.0/tools/ILRepack.exe'),
                   '/log', '/wildcards', '/internalize', '/ndebug',
                   '/out:'+file("src/main/resources/META-INF/bin/ProjectFileParser.exe"),
                   msbuild.mainProject.properties.TargetPath,
                   'Newtonsoft.Json.dll']
}
generateExe.onlyIf { OperatingSystem.current().windows }

nugetRestore {
    solutionFile = file('ProjectFileParser.sln')
}

build.dependsOn generateExe

bintray {
    // to remove when upgraded plugindev to 1.0.4
    user project.properties.bintrayUser
    key project.properties.bintrayApiKey
    pkg.repo = 'gradle-plugins'
    pkg.userOrg = 'ullink'
    pkg.version.gpg.sign = true
}

plugindev {
    pluginId 'com.ullink.msbuild'
    pluginName 'com.ullink.gradle:gradle-msbuild-plugin'
    pluginImplementationClass 'com.ullink.MsbuildPlugin'
    pluginDescription project.description
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'msbuild', 'c#', '.net'
    authorId 'gluck'
    authorName 'Francois Valdy'
    authorEmail 'francois.valdy@gmail.com'
    projectUrl "https://github.com/Ullink/${project.name}"
    projectInceptionYear '2012'
    done()
}


