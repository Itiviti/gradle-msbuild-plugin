plugins {
    id 'nu.studer.plugindev' version '1.0.3'
    id 'com.jfrog.bintray' version '1.2'
    id 'com.ullink.msbuild' version '2.16'
    id 'com.ullink.nuget' version '2.13'
    id 'com.ullink.nunit' version '1.5'
    id 'net.researchgate.release' version '2.3.4'
    id 'de.undercouch.download' version '3.2.0'
}

group = 'com.ullink.gradle'

apply plugin: 'groovy'

description 'Gradle plugin for MSBuild project build.'

ext.dotnetPath = file('src/main/dotnet')

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0', transitive: false
    compile 'net.java.dev.jna:jna:4.2.2'
    compile 'net.java.dev.jna:jna-platform:4.2.2'
    compile 'com.google.guava:guava:16.0.1'
    compile 'commons-io:commons-io:2.4'
}

test {
    if (System.getProperty('testsDebug', 'true').toBoolean()) {
        testLogging {
            exceptionFormat "full"
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            minGranularity 0
        }
    }
}

nuget {
    // still experiencing freezes on mono with nuget 3.3.0
    version = '2.8.6'
}

nugetRestore {
    solutionFile = file('ProjectFileParser.sln')
}

msbuild.dependsOn nugetRestore
msbuild {
    solutionFile = file('ProjectFileParser.sln')
    configuration = 'Release'
    projectName = 'ProjectFileParser'
}

nunit {
    nunitVersion = '3.0.1'
    testAssemblies = [ msbuild.projects['ProjectFileParser_Tests'].properties.TargetPath ]
}
nunit.dependsOn msbuild

task generateZip(dependsOn: nunit, type: Zip) {
    from (msbuild.mainProject.properties.TargetDir) {
        include '*.exe'
        include '*.dll'
        include '*.targets'
        include '*.props'
    }
    into '/'
    archiveName 'ProjectFileParser.zip'
}

task downloadVsWhere(type: de.undercouch.gradle.tasks.download.Download) {
    src 'https://github.com/Microsoft/vswhere/releases/download/2.1.4/vswhere.exe'
    dest "$temporaryDir/vswhere.exe"
}

jar {
    from(generateZip.outputs.files) {
        into ('META-INF')
    }
}

sourceSets {
    main {
        resources {
            srcDirs = [ downloadVsWhere.temporaryDir ]
        }
    }
}

bintray {
    // to remove when upgraded plugindev to 1.0.4
    user project.properties.bintrayUser
    key project.properties.bintrayApiKey
    pkg.repo = 'gradle-plugins'
    pkg.userOrg = 'ullink'
    pkg.version.gpg.sign = true
}

plugindev {
    pluginId 'com.ullink.msbuild'
    pluginName 'com.ullink.gradle:gradle-msbuild-plugin'
    pluginImplementationClass 'com.ullink.MsbuildPlugin'
    pluginDescription project.description
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'msbuild', 'c#', '.net'
    authorId 'gluck'
    authorName 'Francois Valdy'
    authorEmail 'francois.valdy@gmail.com'
    projectUrl "https://github.com/Ullink/${project.name}"
    projectInceptionYear '2012'
    done()
}

processResources.dependsOn downloadVsWhere
afterReleaseBuild.dependsOn bintrayUpload