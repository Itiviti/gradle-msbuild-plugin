plugins {
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'com.itiviti.dotnet' version '2.0.1'
    id 'net.researchgate.release' version '2.6.0'
    id 'de.undercouch.download' version '4.1.2'
}

group = 'com.ullink.gradle'

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'maven-publish'

description 'Gradle plugin for MSBuild project build.'

ext.dotnetPath = file('src/main/dotnet')

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.10.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    implementation 'net.java.dev.jna:jna:4.2.2'
    implementation 'net.java.dev.jna:jna-platform:4.2.2'
    implementation 'com.google.guava:guava:32.0.0-jre'
}

test {
    useJUnitPlatform()
}

dotnet {
    solution = 'ProjectFileParser.sln'
    projectName = 'ProjectFileParser'
}

tasks.test.dependsOn(tasks.named('dotnetTest'))

tasks.register('generateZip', Zip) {
    dependsOn tasks.named('dotnetBuild')
    from { dotnet.mainProject.properties.TargetDir } {
        include '*.exe'
        include '*.json'
        include '*.dll'
    }
    into '/'
    archiveFileName = 'ProjectFileParser.zip'
}

tasks.register('downloadVsWhere', de.undercouch.gradle.tasks.download.Download) {
    src 'https://github.com/Microsoft/vswhere/releases/download/2.8.4/vswhere.exe'
    dest "$temporaryDir/vswhere.exe"
}

jar {
    from(generateZip.outputs.files) {
        into ('META-INF')
    }
}

sourceSets {
    main {
        resources {
            srcDirs = [ downloadVsWhere.temporaryDir ]
        }
    }
}

test {
    useJUnitPlatform()
}

pluginBundle {
    website = 'https://github.com/Itiviti/gradle-msbuild-plugin'
    vcsUrl = 'https://github.com/Itiviti/gradle-msbuild-plugin'
    tags = ['msbuild', 'c#', '.net']

    mavenCoordinates {
        groupId = 'com.ullink.gradle'
    }
}

gradlePlugin {
    plugins {
        msbuildPlugin {
            id = 'com.ullink.msbuild'
            description = project.description
            displayName = 'Gradle MSBuild Plugin'
            implementationClass = 'com.ullink.MsbuildPlugin'
        }
    }
}

processResources.dependsOn downloadVsWhere
afterReleaseBuild.dependsOn publishPlugins
