plugins {
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'com.ullink.msbuild' version '3.14'
    id 'com.ullink.nuget' version '2.23'
    id 'com.ullink.nunit' version '2.4'
    id 'net.researchgate.release' version '2.6.0'
    id 'de.undercouch.download' version '4.1.2'
}

group = 'com.ullink.gradle'

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'maven-publish'

description 'Gradle plugin for MSBuild project build.'

ext.dotnetPath = file('src/main/dotnet')

dependencies {
    testImplementation 'org.spockframework:spock-core:2.0-groovy-2.5'
    testImplementation 'junit:junit:4.12'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.2.0"
    implementation 'net.java.dev.jna:jna:4.2.2'
    implementation 'net.java.dev.jna:jna-platform:4.2.2'
    implementation 'com.google.guava:guava:27.0.1-jre'
}

nugetRestore {
    solutionFile = file('ProjectFileParser.sln')
}

msbuild.dependsOn nugetRestore
msbuild {
    solutionFile = file('ProjectFileParser.sln')
    configuration = 'Release'
    projectName = 'ProjectFileParser'
}

nunit {
    testAssemblies = [ msbuild.projects['ProjectFileParser_Tests'].properties.TargetPath ]
}

task generateZip(dependsOn: nunit, type: Zip) {
    from (msbuild.mainProject.properties.TargetDir) {
        include '*.exe'
        include '**/*.dll'
        include '*.config'
    }
    into '/'
    archiveFileName = 'ProjectFileParser.zip'
}

task downloadVsWhere(type: de.undercouch.gradle.tasks.download.Download) {
    src 'https://github.com/Microsoft/vswhere/releases/download/2.8.4/vswhere.exe'
    dest "$temporaryDir/vswhere.exe"
}

jar {
    from(generateZip.outputs.files) {
        into ('META-INF')
    }
}

sourceSets {
    main {
        resources {
            srcDirs = [ downloadVsWhere.temporaryDir ]
        }
    }
}

test {
    useJUnitPlatform()
}

pluginBundle {
    website = 'https://github.com/Itiviti/gradle-msbuild-plugin'
    vcsUrl = 'https://github.com/Itiviti/gradle-msbuild-plugin'
    tags = ['msbuild', 'c#', '.net']

    mavenCoordinates {
        groupId = 'com.ullink.gradle'
    }
}

gradlePlugin {
    plugins {
        msbuildPlugin {
            id = 'com.ullink.msbuild'
            description = project.description
            displayName = 'Gradle MSBuild Plugin'
            implementationClass = 'com.ullink.MsbuildPlugin'
        }
    }
}

processResources.dependsOn downloadVsWhere
afterReleaseBuild.dependsOn publishPlugins
