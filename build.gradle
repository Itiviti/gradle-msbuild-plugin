import org.gradle.internal.os.OperatingSystem;

plugins {
    id 'nu.studer.plugindev' version '1.0.3'
    id 'com.jfrog.bintray' version '1.2'
    id 'com.ullink.msbuild' version '2.12'
    id 'com.ullink.nuget' version '2.13'
    id 'com.ullink.nunit' version '1.5'
    id 'net.researchgate.release' version '2.3.4'
}

group = 'com.ullink.gradle'

apply plugin: 'groovy'

description 'Gradle plugin for MSBuild project build.'

ext.dotnetPath = file('src/main/dotnet')

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '0.7-groovy-2.0', transitive: false
    compile 'net.java.dev.jna:jna:3.5.0'
    compile 'net.java.dev.jna:platform:3.5.0'
    compile 'com.google.guava:guava:16.0.1'
    compile 'commons-io:commons-io:2.4'
}

nugetRestore {
    solutionFile = file('ProjectFileParser.sln')
}

msbuild.dependsOn nugetRestore
msbuild {
    solutionFile = file('ProjectFileParser.sln')
    configuration = 'Release'
    projectName = 'ProjectFileParser'
}

nunit {
    nunitVersion = '3.0.1'
    testAssemblies = [ msbuild.projects['ProjectFileParser_Tests'].properties.TargetPath ]
}
nunit.dependsOn msbuild

task generateZip(dependsOn: msbuild, type: Zip) {
    from (msbuild.mainProject.properties.TargetDir) {
        include '*.exe'
        include '*.dll'
    }
    into '/'
    destinationDir file('src/main/resources/META-INF/')
    archiveName 'ProjectFileParser.zip'
}

msbuild.onlyIf { OperatingSystem.current().windows }
nunit.onlyIf { OperatingSystem.current().windows }
generateZip.onlyIf { OperatingSystem.current().windows }

jar.dependsOn generateZip

bintray {
    // to remove when upgraded plugindev to 1.0.4
    user project.properties.bintrayUser
    key project.properties.bintrayApiKey
    pkg.repo = 'gradle-plugins'
    pkg.userOrg = 'ullink'
    pkg.version.gpg.sign = true
}

plugindev {
    pluginId 'com.ullink.msbuild'
    pluginName 'com.ullink.gradle:gradle-msbuild-plugin'
    pluginImplementationClass 'com.ullink.MsbuildPlugin'
    pluginDescription project.description
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'msbuild', 'c#', '.net'
    authorId 'gluck'
    authorName 'Francois Valdy'
    authorEmail 'francois.valdy@gmail.com'
    projectUrl "https://github.com/Ullink/${project.name}"
    projectInceptionYear '2012'
    done()
}

project.tasks.afterReleaseBuild.dependsOn project.tasks.bintrayUpload

