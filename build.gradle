plugins {
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'com.ullink.msbuild' version '3.9'
    id 'com.ullink.nuget' version '2.18'
    id 'com.ullink.nunit' version '2.0'
    id 'net.researchgate.release' version '2.6.0'
    id 'de.undercouch.download' version '3.4.3'
}

group = 'com.ullink.gradle'

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'

description 'Gradle plugin for MSBuild project build.'

ext.dotnetPath = file('src/main/dotnet')

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'
    compile gradleApi()
    compile 'net.java.dev.jna:jna:4.2.2'
    compile 'net.java.dev.jna:jna-platform:4.2.2'
    compile 'com.google.guava:guava:27.0.1-jre'
}

nugetRestore {
    solutionFile = file('ProjectFileParser.sln')
}

msbuild.dependsOn nugetRestore
msbuild {
    solutionFile = file('ProjectFileParser.sln')
    configuration = 'Release'
    projectName = 'ProjectFileParser'
}

nunit {
    testAssemblies = [ msbuild.projects['ProjectFileParser_Tests'].properties.TargetPath ]
}

task generateZip(dependsOn: nunit, type: Zip) {
    from (msbuild.mainProject.properties.TargetDir) {
        include '*.exe'
        include '**/*.dll'
        include '*.config'
    }
    into '/'
    archiveFileName = 'ProjectFileParser.zip'
}

task downloadVsWhere(type: de.undercouch.gradle.tasks.download.Download) {
    src 'https://github.com/Microsoft/vswhere/releases/download/2.8.4/vswhere.exe'
    dest "$temporaryDir/vswhere.exe"
}

jar {
    from(generateZip.outputs.files) {
        into ('META-INF')
    }
}

sourceSets {
    main {
        resources {
            srcDirs = [ downloadVsWhere.temporaryDir ]
        }
    }
}

pluginBundle {
    website = 'https://github.com/Itiviti/gradle-msbuild-plugin'
    vcsUrl = 'https://github.com/Itiviti/gradle-msbuild-plugin'
    tags = ['msbuild', 'c#', '.net']

    mavenCoordinates {
        groupId = 'com.ullink.gradle'
    }
}

gradlePlugin {
    plugins {
        msbuildPlugin {
            id = 'com.ullink.msbuild'
            description = project.description
            displayName = 'Gradle MSBuild Plugin'
            implementationClass = 'com.ullink.MsbuildPlugin'
        }
    }
}

processResources.dependsOn downloadVsWhere
afterReleaseBuild.dependsOn publishPlugins
